>  采样环境
>
> 使用反射探针
>
> 创建粗糙和光滑的镜面
>
> 执行盒子投影，对立方体贴图的采样
>
> 混合反射探针

这是渲染系列教程的第八部分。我们在前一部分添加了对阴影的支持。这一部分介绍间接反射。

本教程是用Unity 5.4.0f3制作的。

![有时候你的作品会自我反射](https://catlikecoding.com/unity/tutorials/rendering/part-8/tutorial-image.jpg)

# 环境映射

目前，我们的着色器通过结合环境、漫反射和镜面反射来着色一个片段。这产生了看起来很真实的图像，至少当表面是暗淡的时候。然而，有光泽的表面看起来就不是那么的好了。

有光泽的表面就像镜子，尤其是金属的表面。完美的镜子反射所有的光。这意味着根本没有漫反射。它只是镜面反射。让我们把材质变成一面镜子，通过设置金属为1，平滑为0.95。把它变成纯白色。

![一个闪亮的白色金属球](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/shiny.png)

结果是一个几乎完全黑色的表面，尽管它的颜色是白色的。我们只看到一个小亮点，光源直接反射到我们。所有其他的光都被反射到不同的方向。如果将平滑度增加到1，那么高光也会消失。

这看起来一点也不像真正的镜子。镜子不是黑色的，它们反射东西!在这种情况下，它应该反射天空盒，显示蓝色的天空与灰色的地面。

## 间接的高光反射

我们的球体变成了黑色，因为我们只包括了直射光。为了反射环境，我们还必须包括间接光。具体地说，是用于镜面反射的间接光。在CreateIndirectLight函数中，我们配置了Unity的UnityIndirect结构。到目前为止，我们已经把它的镜面分量设为零。这就是球体变成黑色的原因!

设置场景的环境强度为零，这样我们就可以专注于反射。把我们的材质再次变成暗淡的非金属，平滑度为0.5。然后将间接高光的颜色改为明显的颜色，比如红色。

```c
UnityIndirect CreateIndirectLight (Interpolators i) {
	UnityIndirect indirectLight;
	indirectLight.diffuse = 0;
	indirectLight.specular = 0;

	#if defined(VERTEXLIGHT_ON)
		indirectLight.diffuse = i.vertexLightColor;
	#endif

	#if defined(FORWARD_BASE_PASS)
		indirectLight.diffuse += max(0, ShadeSH9(float4(i.normal, 1)));
		indirectLight.specular = float3(1, 0, 0);
	#endif

	return indirectLight;
}
```

![black](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/black.png)

![red](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/red.png)

球体染上了一种红色。在这种情况下，红色表示反射率。所以我们的球体从中心向我们反射一些环境光。显然，它的边缘反射更多。这是因为当视角变得更浅时，每个表面都变得更具反射性。从侧视的角度看，大多数的光都被反射了，所有的东西都变成了镜子。这就是著名的菲涅耳反射。我们使用的UNITY_BRDF_PBS为我们计算它。

表面越光滑，菲涅尔反射越强。当使用高平滑度时，红色的圆环变得非常明显。

![0.15](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/smoothness-15.png)

![0.95](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/smoothness-95.png)

由于反射来自间接光，所以它与直接光源无关。因此，反射也独立于光源的阴影。因此，在球体的边缘，菲涅耳反射变得非常明显。

就金属而言，间接反射无处不在。我们现在得到的不是一个黑色的球体，而是一个红色的球体。

![0.15](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/metallic-smoothness-15.png)

![0.50](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/metallic-smoothness-50.png)

![0.95](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/metallic-smoothness-95.png)

## 采样环境

为了反映实际环境，我们必须对skybox立方体贴图进行采样。在UnityShaderVariables中定义为unity_SpecCube0。这个变量的类型取决于目标平台，这是在HSLSupport中确定的。

立方体映射使用3D矢量进行采样，该矢量指定采样方向。我们可以使用UNITY_SAMPLE_TEXCUBE宏来处理类型差异。我们先用法向量作为样本方向。

```c
#if defined(FORWARD_BASE_PASS)
		indirectLight.diffuse += max(0, ShadeSH9(float4(i.normal, 1)));
		float3 envSample = UNITY_SAMPLE_TEXCUBE(unity_SpecCube0, i.normal);
		indirectLight.specular = envSample;
	#endif
```

![环境采样](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/environment-sample.png)

天空盒出现了，但它太亮了。这是因为立方体贴图包含高动态范围的颜色，这使得它可以包含大于1的亮度值。我们必须将示例从HDR格式转换为RGB格式。

UnityCG包含DecodeHDR函数，我们可以使用。HDR数据使用RGBM格式存储在四个通道中。所以我们必须采样一个float4值，然后进行转换。

```c
indirectLight.diffuse += max(0, ShadeSH9(float4(i.normal, 1)));
		float4 envSample = UNITY_SAMPLE_TEXCUBE(unity_SpecCube0, i.normal);
		indirectLight.specular = DecodeHDR(envSample, unity_SpecCube0_HDR);
```

![HDR解码](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/decode-hdr.png)

## 跟踪反射

我们得到了正确的颜色，但是我们还没有看到真实的反射。因为我们使用球体的发现来采样环境，投影不依赖于视图方向。它就像一个画着环境贴图的球体。

为了产生真实的反射，我们必须将相机的方向移到表面，然后使用法线进行反射。我们可以使用反射函数，就像我们在第4部分中做的那样，第一束光。在本例中，我们需要视图方向，因此将其作为参数添加到CreateIndirectLight中。

```c
UnityIndirect CreateIndirectLight (Interpolators i, float3 viewDir) {
	…
	
	#if defined(FORWARD_BASE_PASS)
		indirectLight.diffuse += max(0, ShadeSH9(float4(i.normal, 1)));
		float3 reflectionDir = reflect(-viewDir, i.normal);
		float4 envSample = UNITY_SAMPLE_TEXCUBE(unity_SpecCube0, reflectionDir);
		indirectLight.specular = DecodeHDR(envSample, unity_SpecCube0_HDR);
	#endif

	return indirectLight;
}

…

float4 MyFragmentProgram (Interpolators i) : SV_TARGET {
	…

	return UNITY_BRDF_PBS(
		albedo, specularTint,
		oneMinusReflectivity, _Smoothness,
		i.normal, viewDir,
		CreateLight(i), CreateIndirectLight(i, viewDir)
	);
}
```

![normal](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/normal.png)

![reflection](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/relfection.png)

## 使用反射探针

反射天空盒是不错的，但它如果反射实际的场景几何就更好了。让我们创建一个旋转的方平面作为地板，并在上面放置了一些立柱，在立柱上还有一些立柱。球体在建筑物的中心盘旋。

![反射一些物体](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/building.png)

要看到建筑物的反射，我们得先把它拍下来。这是通过反射探头完成的，你可以通过GameObject / Light / reflection probe来添加反射探头。创建一个并把它放在与我们的球体相同的位置。

![gizmo](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/reflection-probe-gizmo.png)

![inspector](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/reflection-probe-inspector.png)

场景视图用一个圆形的小图标指示反射探针的存在。它的外观取决于场景视图的配置。如果这个小玩意挡住了球体的视线，我们把它关掉。你可以打开场景视图工具栏的小玩意下拉菜单，向下滚动到ReflectionProbe，然后点击它的图标。

![on](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/gizmo-on.png)

![off](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/gizmo-off.png)

反射探测器通过渲染一个立方体贴图来捕获环境。这意味着它渲染场景6次，每个立方体面一次。默认情况下，它的类型设置为bake。在此模式下，多维数据集映射由编辑器生成并包含在构建中。这些贴图只包括静态几何。所以我们的建筑在被渲染到立方体贴图之前必须是静态的。

或者，我们可以将反射探针的类型更改为Realtime。这样的探针是在运行时渲染的，您可以选择何时渲染，多久渲染一次。还有一个自定义模式，让你完全控制。

虽然实时探针是最灵活的，但如果频繁更新，它也是最昂贵的。此外，实时探针在编辑模式下不更新，而烘焙探针在编辑它们或静态几何图形时更新。所以让我们坚持使用烘焙过的探针，同时让我们的建筑保持是静态。

对象实际上不需要完全是静态的。为了各种子系统的目的，您可以将它们标记为静态的。在这种情况下，相关的设置是反射探针静态。当启用时，对象被渲染到烘焙过的探针中。你可以在运行时移动它们，但是它们的反射仍然是静止的。

![静态反射探针](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/static.png)

当建筑物被标记为静态后，反射探头将被更新。它会出现一会儿的黑色，然后反射再出现。反射球不是反射本身的一部分，所以让它保持为动态。

![反射几何](https://catlikecoding.com/unity/tutorials/rendering/part-8/environment-mapping/reflected-geometry.png)

## 不完美的反射

只有非常光滑的表面才能产生非常锐利的反射。一个表面变得越粗糙，它的反射就得到更多的漫反射。暗淡的镜子产生模糊的反射。我们怎样才能使反射变得模糊呢？

纹理可以有mipmaps，这是原始图像的下采样版本。当以全尺寸观看时，更高的mipmaps因此产生更模糊的图像。这些可能是块图像，但Unity使用不同的算法来生成环境贴图的mipmaps。这些卷积的贴图很好的代表了一个从尖锐到模糊过程。

![0](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/lod-0.png)

![1](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/lod-1.png)

![![3](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/lod-3.png)2](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/lod-2.png)

![4](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/lod-4.png)

![5](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/lod-5.png)

## 粗糙的镜子

我们可以使用UNITY_SAMPLE_TEXCUBE_LOD宏来采样特定mipmap级别的立方体贴图。环境立方体贴图使用三线性过滤，因此我们可以混合相邻的层次。这使得我们可以根据材质的平滑程度来选择mipmap。材料越粗糙，我们应该使用的mipmap级别就越高。

当粗糙度从0到1时，我们必须按我们使用的mipmap范围进行缩放。Unity使用UNITY_SPECCUBE_LOD_STEPS宏来确定这个范围，所以我们也使用它。

```c
float roughness = 1 - _Smoothness;
		float4 envSample = UNITY_SAMPLE_TEXCUBE_LOD(
			unity_SpecCube0, reflectionDir, roughness * UNITY_SPECCUBE_LOD_STEPS
		);
```

![光滑度为0.5](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/roughness.png)

实际上，粗糙度与mipmap级别之间并不是线性关系。Unity使用的换算公式为$1.7r-0.7r^2$。

![1.7r−0.7r2 vs. linear ](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/roughness-mapping.png)

```c
float roughness = 1 - _Smoothness;
		roughness *= 1.7 - 0.7 * roughness;
		float4 envSample = UNITY_SAMPLE_TEXCUBE_LOD(
			unity_SpecCube0, reflectionDir, roughness * UNITY_SPECCUBE_LOD_STEPS
		);
```

![0.50](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/smoothness-50.png)

![0.75](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/smoothness-75.png)

![0.95](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/smoothness-95.png)

UnityStandardBRDF包含文件包含Unity_GlossyEnvironment函数。它包含转换粗糙度、采样立方体贴图和从HDR转换的所有代码。让我们用这个函数来代替我们自己的代码。

要将立方体贴图作为参数传递，我们必须使用UNITY_PASS_TEXCUBE宏。这将处理类型差异。此外，粗糙度和反射方向必须封装在Unity_GlossyEnvironmentData结构中。

```c
indirectLight.diffuse += max(0, ShadeSH9(float4(i.normal, 1)));
		float3 reflectionDir = reflect(-viewDir, i.normal);
//		float roughness = 1 - _Smoothness;
//		roughness *= 1.7 - 0.7 * roughness;
//		float4 envSample = UNITY_SAMPLE_TEXCUBE_LOD(
//			unity_SpecCube0, reflectionDir, roughness * UNITY_SPECCUBE_LOD_STEPS
//		);
//		indirectLight.specular = DecodeHDR(envSample, unity_SpecCube0_HDR);
		Unity_GlossyEnvironmentData envData;
		envData.roughness = 1 - _Smoothness;
		envData.reflUVW = reflectionDir;
		indirectLight.specular = Unity_GlossyEnvironment(
			UNITY_PASS_TEXCUBE(unity_SpecCube0), unity_SpecCube0_HDR, envData
		);
```

## 凹凸不平的镜面

除了使用平滑度来表示粗糙的镜面外，您当然也可以使用法线贴图来添加更大的变形。当我们使用扰动的法线来确定反射方向时，这是可行的。

![0.50](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/bumped-50.png)

![0.75](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/bumped-75.png)

![0.95](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/bumped-95.png)

## 金属与非金属

金属和非金属表面都能产生清晰的反射，只是它们看起来不同。高光反射在有光泽的介电材料上可以表现得很好，但它们并不支配它们的外观。仍然可以看到大量的漫反射。

![0.50](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/nonmetal-smoothness-50.png)

![0.75](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/nonmetal-smoothness-75.png)

![0.95](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/nonmetal-smoothness-95.png)

回想一下，金属会给它的镜面反射上色，而非金属不会。这适用于高光，也适用于高光环境反射。

![metal](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/red-metal.png)

![nonmetal](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/red-nonmetal.png)

## 镜面和反射

如前所述，间接反射与表面的直接照明无关。这是对于其他阴影区域是最明显的。在非金属的情况下，这只是导致在视觉上更明亮的表面。你仍然可以看到由直射光投射出的阴影。

![0.50](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/shadowed-nonmetal-50.png)

![0.75](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/shadowed-nonmetal-75.png)

![1.00](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/shadowed-nonmetal-100.png)

同样的规则也适用于金属，但主要是间接反射。因此，直接的光和阴影会随着亮度的增加而消失。完美的镜子上没有阴影。

![0.50](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/shadowed-metal-50.png)

![0.75](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/shadowed-metal-75.png)

![1.00](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/shadowed-metal-100.png)

虽然这在物理上是正确的，但现实生活很少是完美的。例如，你可以直接看到光和阴影在粘在完美镜子上的污垢和灰尘上。还有许多材料是金属和介电材料的混合物。您可以通过将金属滑块设置在0和1之间来模拟这一点。

![金属度为0.75，一块脏的镜子](https://catlikecoding.com/unity/tutorials/rendering/part-8/imperfect-reflections/dusty-mirror.png)

# 盒子投影

我们目前有一个反射球和一个反射探头。两者都在我们大楼的中心盘旋。让我们添加更多的球体，把它们放在靠近内正方形区域的边缘。但我们只关注中心的一个探针。

![所有的反射都是一样的](https://catlikecoding.com/unity/tutorials/rendering/part-8/box-projection/same-reflections.png)

反射看起来有些问题。它们看起来都一样。视角略有不同，但所有的球体都反映了环境，就好像它们位于建筑的中心。它们不是，但反射探测器是!

如果我们想要更真实的反射，那么我们必须为每个球体创建一个探针，并将它们放置在适当的位置。这样，每个球体都可以从自己的角度得到一个环境贴图。

![每个球一个探针，不同的反射](https://catlikecoding.com/unity/tutorials/rendering/part-8/box-projection/different-reflections.png)

虽然这是更好的，但仍然不是完全现实的。为了实现这一点，我们必须为渲染的每个片段使用一个反射探针。这将需要在球体表面放置许多探针。幸运的是，这个近似对球体来说不是太坏。但是对于平面的镜子呢?

首先，除去中心反射探头以外的所有探头。然后创建一个方庭，并将其放置在建筑内部，接触到柱子的中点。把它变成一面镜子，观察它的反射。

![不正确的地板反射](https://catlikecoding.com/unity/tutorials/rendering/part-8/box-projection/floor-mirror-wrong.png)

倒影完全不匹配!方向看起来是正确的，但是比例和位置是错误的。如果我们在每个片段上使用一个探针，反射就会很好。但我们只有一个探针。这个近似值对于那些实际上无限远的物体是足够的，比如skybox。但它不适用于附近事物的反射。

当一个环境的一部分无限远时，我们在确定它的反射时不关心视图的位置。但当大部分环境都在附近时，我们就会关心。假设我们在一个空的立方体房间中间有一个反射探头。它的环境贴图包含了这个房间的墙壁、地板和天花板。如果立方体贴图和房间是对齐的，那么立方体贴图的每个面都与墙壁、地板或天花板中的一个完全对应。

接下来，假设我们在这个房间的任何地方都有一个表面位置和一个反射方向。这个向量最终会与立方体的某条边相交。我们可以用一些数学计算这个交点。然后我们可以构建一个向量从房间的中心到这一点。使用这个向量，我们可以对立方体贴图进行采样，最终得到正确的反射。

![投影来找到样本的方向](https://catlikecoding.com/unity/tutorials/rendering/part-8/box-projection/projecting.png)

房间不需要是一个立方体。任何长方形都可以，就像我们的建筑内部一样。然而，房间和立方体贴图必须对齐。

## 反射探针盒

反射探针具有大小和探针原点，它们定义了世界空间中相对于其位置的立方体区域。它总是轴向的，这意味着它忽略了所有的旋转。它也忽略了缩放。

这个区域有两个用途。首先，Unity使用这些区域来决定渲染对象时使用哪个探测。第二，这个区域是用来做盒子投影的，这就是我们要做的。

您可以在场景视图中显示该框时，你有探头选择。在反射探测器的检查器的顶部是一个探测器场景编辑模式切换。左边的按钮打开了盒子投影边界的Gizmos。

![scene](https://catlikecoding.com/unity/tutorials/rendering/part-8/box-projection/bounds-scene.png)

![inspector](https://catlikecoding.com/unity/tutorials/rendering/part-8/box-projection/bounds-inspector.png)

您可以使用边界面中心的黄点来调整它们。您还可以通过编辑大小和在检视面板中探针原点向量来调整它们。通过调整原点，可以将方框相对于采样点移动。你也可以在场景中用其他的编辑模式来调整它，但是它很麻烦，而且现在用撤销功能还不能很好地工作。

调整盒子，使它覆盖建筑内部，触及柱子并一直延伸到最高点。我把它做得稍微大一点，以防止在场景视图中由于小发明的Z-fighting而导致闪烁。

![scene](https://catlikecoding.com/unity/tutorials/rendering/part-8/box-projection/adjusted-bounds-scene.png)

![inspector](https://catlikecoding.com/unity/tutorials/rendering/part-8/box-projection/adjusted-bounds-inspector.png)

当我们在做的时候，启用盒子投影，因为那是我们想要做的。

## 调整采样方向

为了计算盒投影，我们需要初始反射方向、采样位置、立方体贴图位置和盒边界。为我们的着色器添加一个函数，在CreateIndirectLight的上面。

```c
float3 BoxProjection (
	float3 direction, float3 position,
	float3 cubemapPosition, float3 boxMin, float3 boxMax
) {
	return direction;
}
```

首先，调整边界，使它们相对于表面位置。

```c
	boxMin -= position;
	boxMax -= position;
	return direction;
```

接下来，我们要缩放方向向量，使它从位置移动到想要的交点。我们先考虑X维。如果方向的X分量是正的，那么它指向最大值。否则，它指向最小界限。用适当的界限除以方向的X分量就得到了我们需要的标量。当方向为负时也是这样，因为最小界限也是负的，在除法后得到正的结果。

```c
	boxMin -= position;
	boxMax -= position;
	float x = (direction.x > 0 ? boxMax.x : boxMin.x) / direction.x;
```

对于Y和Z维度也是一样的。

```c
	float x = (direction.x > 0 ? boxMax.x : boxMin.x) / direction.x;
	float y = (direction.y > 0 ? boxMax.y : boxMin.y) / direction.y;
	float z = (direction.z > 0 ? boxMax.z : boxMin.z) / direction.z;
```

我们现在有三个标量，但哪个是正确的呢?这取决于哪个标量是最小的。表示哪个边界面最接近。

![选择最小的因数](https://catlikecoding.com/unity/tutorials/rendering/part-8/box-projection/smallest-factor.png)

```c
	float z = (direction.z > 0 ? boxMax.z : boxMin.z) / direction.z;
	float scalar = min(min(x, y), z);
```

现在我们可以通过将缩放后的方向加到位置上来找到交点。通过减去立方体贴图的位置，我们得到了新的投影样本方向。