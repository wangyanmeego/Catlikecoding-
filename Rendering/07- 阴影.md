> 研究Unity如何渲染阴影
>
> 投射直射阴影
>
> 接收直射阴影
>
> 添加对聚光灯和点光灯阴影的支持

这是渲染教程系列的第七部分。前一部分讨论了法线映射。现在我们来看看阴影。

本教程是用Unity 5.4.0f3制作的。

![渲染时，投射阴影是一件很酷的事情](https://catlikecoding.com/unity/tutorials/rendering/part-7/tutorial-image.jpg)

# 直射阴影

虽然到目前为止我们的光照着色器产生了相当真实的结果，它评估每个孤立的表面片段。它假设来自每个光源的一束光最终击中每个片段。但只有当这些光线没有被什么东西挡住时，这才成立。

![一些光线被遮挡](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/blocked-light.png)

当一个物体位于光源和另一个物体之间时，它可能会阻止部分或全部光线到达另一个物体。照亮第一个物体的光线不再能照亮第二个物体。因此，第二个物体将保持至少部分接收不到光。未被照亮的区域位于第一个物体的阴影中。为了描述这个现象，我们常说第一个物体在第二个物体上投下阴影。

实际上，在完全光照和完全阴影的空间之间有一个过渡区域，称为半阴影。它的存在是因为所有的光源都有一个体积。因此，有些区域只有部分光源可见，这意味着它们被部分遮挡。光源越大，一个表面离它的阴影投射者越远，这个区域就越大。

![img](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/penumbra.png)

Unity不支持半阴影。Unity确实支持软阴影，但那是一种阴影滤波技术，不是半阴影的模拟。

## 启用阴影

没有阴影，就很难看到物体之间的空间关系。为了说明这一点，我用几个拉伸的立方体创建了一个简单的场景。我在这些立方体上面放了四排球体。中间几排漂浮着球体，而外面几排通过圆柱体与下面的立方体相连。

这些物体有Unity默认的白色材质。场景有两个方向灯，默认方向灯和稍微弱一点的黄色灯。这些是在以前的教程中使用过的相同的灯。

目前，阴影在当前项目范围内是禁用的。我们在以前的教程中做过。环境强度也设置为零，这样更容易看到阴影。

![两个方向的光，没有阴影，没有环境光](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/without-shadows.png)

阴影是项目范围内的质量设置的一部分，可以通过*Edit / Project Settings / Quality*找到。我们将使他们达到高质量级别。这意味着同时支持硬阴影和软阴影，使用高分辨率、稳定的拟合投影、150的距离和四个cascades。

![阴影质量设置](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/shadows-enabled.png)

确保两盏灯都设置了软阴影。它们的分辨率应该取决于质量设置。

![每盏灯的阴影设置](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/light-settings.png)

两个方向的光都投射阴影，所有物体之间的空间关系变得更加清晰。整个场景变得更加真实和有趣。

![带有阴影的场景](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/with-shadows.png)

## 阴影映射

Unity是如何将这些阴影添加到场景中的?标准的着色器显然有一些方法来确定射线是否被阻挡。

你可以通过在场景中投射射线，从光源到表面片段，来判断一个点是否在阴影中。如果射线在到达片段之前击中了什么东西，那么它就被挡住了。这是一个物理引擎可以做到的事情，但是对每一个片段和每一束光这样做是非常不切实际的。然后你必须以某种当时把结果传给GPU。

有一些技术支持实时阴影。每个都有它的优点和缺点。Unity使用目前最常用的技术，那就是阴影映射。这意味着Unity以某种方式在纹理中存储阴影信息。现在我们将研究它是如何工作的。

通过*Window / Frame Debugger*打开帧调试器，启用它，然后查看渲染步骤的层次结构。看看没有阴影和启用了阴影的帧之间的区别。

![without](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/render-steps-without.png)

![with](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/render-steps-with.png)

当阴影被禁用时，所有对象都像往常一样渲染。我们对这个过程已经很熟悉了。但是如果启用了阴影，这个过程就会变得更加复杂。还有更多的渲染阶段，以及更多的draw call。阴影是很耗性能的。

## 渲染到深度纹理中

当直射阴影被激活时，Unity就会在渲染过程中开始一个深度pass。结果被放入一个与屏幕分辨率匹配的纹理中。此遍历渲染整个场景，但只记录每个片段的深度信息。这是与被GPU用来确定是否一个片段在先前渲染的片段上面或下面相同的信息。

此数据对应于片段在裁剪空间中的Z坐标。这是定义摄像机可以看到的区域的空间。深度信息最终存储为0-1范围内的值。当查看纹理时，附近的纹理显示为黑色。像素越远，颜色就越浅。

![深度纹理，与相机附近的平面设置为5。](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/depth-texture.png)

**什么是裁剪空间？**

这个空间决定了相机看到什么。当你在场景视图中选择主摄像头时，你会看到它前面的一个金字塔线框，这表明它可以看到什么。

![具有大的近平面值的摄像机视图](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/clip-space.png)

在裁剪空间中，这个金字塔是一个规则的立方体。模型-视图-投影矩阵用于将网格顶点转换到这个空间。它被称为剪辑空间，因为在这个立方体之外的所有东西都会被剪辑，因为它是不可见的。

这个信息实际上与阴影没有直接的关系，但是Unity会在以后的pass中使用它。

## 渲染到阴影纹理

Unity渲染的下一件事情是第一盏灯的阴影贴图。稍后，它也会渲染第二个光源的阴影贴图。

同样，整个场景被渲染，只有深度信息被存储在纹理中。然而，这一次场景是从光源的视角渲染的。实际上，光就像照相机一样。这意味着深度值告诉我们一束光在击中某物之前走了多远。这可以用来确定什么东西被遮蔽了!

因为我们使用直射光，他们的相机是正交投影的。因此，没有透视投影，光的相机的确切位置并不重要。Unity将定位相机，使它看到在正常的相机视角下所有的对象。

![1](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/shadow-map-1.png)

![2](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/shadow-map-2.png)

事实上，Unity并不只是每个灯光渲染整个场景。场景被每个灯光渲染四次!纹理被分成四个象限，每个象限从不同的角度渲染。这是因为我们选择了使用四个 shadow cascades。如果你切换到两个 shadow cascades，场景将被渲染两次。如果没有 shadow cascades，每盏灯只能渲染一次。当我们观察阴影的质量时，我们将会看到为什么Unity会这样做。

## 收集阴影

从摄像机的角度，我们得到了场景的深度信息。我们也从每一个灯光的角度得到这个信息。当然，这些数据存储在不同的裁剪空间中，但是我们知道这些空间的相对位置和方向。我们可以从一个空间转换到另一个空间。这使我们能够从两个角度比较深度测量。从概念上讲，我们有两个向量，它们应该在同一点结束。如果它们这样做了，摄像机和光都能看到那个点，所以它被照亮了。

![1](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/screenspace-shadow-map-1.png)

![2](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/screenspace-shadow-map-2.png)

Unity通过渲染一个覆盖整个视图的方块来创建这些纹理。它使用了*Hidden/Internal-ScreenSpaceShadows*着色器来作为这一个pass。每个片段从场景和灯光的深度纹理中取样，进行比较，并将最终的阴影值渲染到屏幕空间的阴影贴图上。有光照的像素设置为1，阴影的像素设置为0。此时Unity也可以执行过滤，创建柔和的阴影。

## 对阴影贴图采样

最后，Unity完成了阴影的渲染。只做了一个更改，现在场景正常被渲染。光的颜色与阴影贴图中存储的值相乘。这就消除了应该被阻挡的光线。

每个片段通过采样阴影贴图被渲染了。还有那些最终隐藏在其他对象后面的片段会稍后被绘制。所以这些片段最终会接收到隐藏它们的物体的阴影。在单步执行帧调试器时可以看到这一点。您还可以看到阴影出现在实际投射它们的对象之前。当然，这些错误只会在渲染帧的过程中出现。一旦完成，图像就是正确的。

![img](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/sampling-shadows.png)

## 阴影质量

当场景从光的角度渲染时，方向与场景摄像机不匹配。因此阴影贴图的纹理与最终图像的纹理不一致。阴影贴图的分辨率也不同。最终图像的分辨率由显示设置决定。阴影贴图的分辨率是由阴影质量设置决定的

当阴影贴图的像素最终渲染得比最终的图像大时，它们将变得很明显。阴影的边缘将被混叠。这在使用硬阴影时最明显。

![hard](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/hard.png)

![soft](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/soft.png)

为了使这个看起来尽可能的明显，改变阴影质量设置，这样在最低的分辨率，没有 cascades下，我们只能得到硬阴影。

![低质量的阴影](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/low-quality.png)

现在看起来很明显。而且，一些阴影出现在不该出现的地方。我们待会再谈。

阴影越接近场景摄像机，它们的像素就越大。这是因为阴影贴图目前覆盖了场景摄像机可见的整个区域。我们可以通过质量设置来减少阴影覆盖的区域，从而增加相机附近的质量。

![阴影距离减少到25。](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/shadow-distance-25.png)

通过将阴影限制在场景摄像机附近的区域，我们可以使用相同的阴影贴图来覆盖更小的区域。结果，我们得到了更好的阴影。但我们失去了更远的影子。当影子接近最大距离时，它们就逐渐消失了。

理想情况下，我们得到高质量的近距离阴影，同时也保持高质量的远距离阴影。因为远处的阴影最终被渲染到一个更小的屏幕区域，所以那些可以用一个低分辨率的阴影贴图。这就是 shadow cascades的作用。当启用时，多个阴影贴图被渲染到相同的纹理中。每一张贴图都是在一定距离内使用的。

![使用四个cascades的低分辨率纹理](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/shadow-cascades.png)

当使用四个cascades时，结果看起来要好得多，即使我们仍然使用相同的纹理分辨率。我们只是更有效地使用了像素。缺点是我们现在必须渲染场景三次以上。

当渲染到屏幕空间的阴影纹理时，Unity负责从正确的cascade中采样。你可以找到一个cascade的结束和另一个开始，通过寻找阴影像素大小的突然变化的地方。

你可以通过质量设置来控制cascade bands的范围，比如阴影距离的部分。你也可以在场景视图中可视化它们，通过改变它的阴影模式。而不是只是阴影，使用*Miscellaneous / Shadow Cascades*。这将在场景的前面渲染cascades 的颜色。

![级联区域，调整为 show three bands](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/cascades-visualized.png)

cascade bands的形状取决于阴影投影质量的设置。默认是*Stable Fit*。在这种模式下， bands是根据到相机位置的距离来选择的。另一个选项是Close Fit，它使用相机的深度。这将在摄像机的视图方向上产生矩形带。

![Close fit](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/close-fit.png)

这种配置允许更有效地使用阴影纹理，从而产生更高质量的阴影。然而，阴影投影现在取决于相机的位置和方向。因此，当相机移动或旋转时，阴影纹理也会改变。如果你能看到阴影纹理，你会注意到它们在移动。这种效果被称为阴影边缘游动，这有可能很明显。这就是为什么另一种模式是默认的。

<iframe src="https://thumbs.gfycat.com/MassiveLeafyHamster-mobile.mp4"></iframe>
## 阴影的痤疮

当我们使用低质量的硬阴影时，我们看到一些阴影出现在不该出现的地方。不幸的是，无论质量设置如何，这种情况都可能发生。

阴影纹理中的每个像素表示光线照射到表面的点。然而，像素不是单点的。它们最终覆盖了更大的区域。它们与光的方向一致，而不是与表面一致。因此，它们最终会像黑色碎片一样粘在表面上，穿过表面，再从表面钻出来。当纹理的一部分从投射出阴影的表面伸出时，表面看起来也会产生阴影。这就是所谓的阴影痤疮。

![阴影纹理导致的痤疮](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/acne-diagram.png)

阴影痤疮的另一个来源是数值精度的限制。当涉及到非常小的距离时，这些限制可能导致错误的结果。

![严重的痤疮，当没有使用任何biases时](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/acne.png)

防止这个问题的一种方法是在渲染阴影映射时添加深度偏移量。这个偏差增加了从光到阴影投射表面的距离，将阴影推入表面。

![有偏差的阴影纹理](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/bias-diaram.png)

![每盏灯的阴影设置](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/light-settings.png)

低偏差会产生阴影痤疮，但大偏差会带来另一个问题。当投射阴影的物体被推离光源时，它们的阴影也被推离光源。因此，阴影不会与物体完全对齐。当使用一个小偏差时，这并不是那么糟糕。但当偏差太大时，阴影会与投射它们的物体分离，这就是所谓的peter panning。

![巨大的偏差导致peter panning](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/peter-panning.png)

除了这个距离偏差，还有一个正常偏差。这是一个微妙的调整阴影脚轮。这种偏差将阴影脚轮的顶点推向内，沿着它们的法线。这也减少了自阴影，但它也使阴影更小，可以导致洞出现在阴影。

**什么是最好的偏差设置？**

没有最好的设置。不幸的是，你必须实验。Unity的默认设置可能有效，但它们也可能产生不可接受的结果。不同的质量设置也会产生不同的结果。

## 抗锯齿处理

你是否在质量设置中是否启用了抗锯齿?如果你有，那么你可能已经发现了阴影纹理的另一个问题。它们不与标准的反锯齿方法混合。

![scene](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/aliasing.png)

![settings](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/msaa-settings.png)

当你在质量设置中启用抗锯齿时，Unity将使用多采样抗锯齿，MSAA。它通过沿三角形边缘执行一些超级采样来消除三角形边缘上的混叠。细节在这里不细述了。重要的是当Unity渲染屏幕空间阴影纹理时，它是通过一个覆盖整个视图的四轴渲染来实现的。因此，没有三角形边，因此MSAA不影响屏幕空间阴影纹理。MSAA确实对最终的图像有效，但是阴影值直接取自屏幕空间阴影纹理。这变得非常明显，当一个较暗表面旁边的灯光表面是阴影。明暗几何图形之间的边缘是反锯齿的，而阴影边缘则不是。

![no aa](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/no-aa.png)

![msaa](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/msaa.png)

![fxaa](https://catlikecoding.com/unity/tutorials/rendering/part-7/directional-shadows/fxaa.png)

依赖于图像后处理的抗锯齿方法——比如FXAA——没有这个问题，因为它们是在整个场景渲染之后应用的。

# 投射阴影

现在我们已经知道了Unity是如何创建直射光的阴影的，是时候在我们自己的着色器中添加对它们的支持了。目前，我的第一个光照着色器既不投射也不接收阴影。

让我们先来处理投射阴影。我改变了示例场景中的球体和圆柱体，所以它们使用了我们的材质。所以现在他们不再投下阴影。

![我们的材质不会投下阴影。](https://catlikecoding.com/unity/tutorials/rendering/part-7/casting-shadows/no-shadows.png)

我们知道Unity会为直射阴影渲染多次场景。一次用于深度pass，一次用于每个阴影贴图cascade。屏幕空间阴影贴图是一个屏幕空间效果，与我们无关。

为了支持所有相关的pass，我们必须添加一个通道到我们的着色器，它的LightMode设置为ShadowCaster。因为我们只对深度值感兴趣，所以它比我们的其他pass要简单得多。

```c
SubShader {

		Pass {
			Tags {
				"LightMode" = "ForwardBase"
			}

			…
		}

		Pass {
			Tags {
				"LightMode" = "ForwardAdd"
			}

			…
		}

		Pass {
			Tags {
				"LightMode" = "ShadowCaster"
			}

			CGPROGRAM

			#pragma target 3.0

			#pragma vertex MyShadowVertexProgram
			#pragma fragment MyShadowFragmentProgram

			#include "My Shadows.cginc"

			ENDCG
		}
	}
```

让我们给影子程序t它们自己的包含文件，命名为My shadow .cginc。它们非常简单。顶点程序像往常一样将位置从对象空间转换为裁剪空间，不做其他任何事情。fragment程序实际上不需要做任何事情，所以只返回0。GPU为我们记录深度值。

```c
#if !defined(MY_SHADOWS_INCLUDED)
#define MY_SHADOWS_INCLUDED

#include "UnityCG.cginc"

struct VertexData {
	float4 position : POSITION;
};

float4 MyShadowVertexProgram (VertexData v) : SV_POSITION {
	return mul(UNITY_MATRIX_MVP, v.position);
}

half4 MyShadowFragmentProgram () : SV_TARGET {
	return 0;
}

#endif
```

这已经足够直射光投射阴影了。

![投射阴影](https://catlikecoding.com/unity/tutorials/rendering/part-7/casting-shadows/casting-shadows.png)

## 偏移

我们还必须支持影子偏移。在深度pass中，偏移为零，但在渲染阴影纹理时，偏移与光线设置相对应。我们通过在裁剪空间中对顶点着色器的位置应用深度偏移来做到这一点

为了支持深度偏移，我们可以使用UnityCG中定义的UnityApplyLinearShadowBias函数。

```c
float4 MyShadowVertexProgram (VertexData v) : SV_POSITION {
	float4 position = mul(UNITY_MATRIX_MVP, v.position);
	return UnityApplyLinearShadowBias(position);
}
```

为了支持法线偏移，我们必须基于法线向量移动顶点位置。所以我们要把法线加到顶点数据中。然后我们可以使用UnityClipSpaceShadowCasterPos函数来应用偏移。这个函数在UnityCG中也有定义。

```c
struct VertexData {
	float4 position : POSITION;
	float3 normal : NORMAL;
};

float4 MyShadowVertexProgram (VertexData v) : SV_POSITION {
	float4 position = UnityClipSpaceShadowCasterPos(v.position.xyz, v.normal);
	return UnityApplyLinearShadowBias(position);
}
```

我们的着色器现在是一个全功能的阴影投射了。

# 接收阴影

第二个人步骤就是接收阴影。测试场景中的所有对象现在都使用我们的材质。

![当只使用我们的材质时，不会接收阴影](https://catlikecoding.com/unity/tutorials/rendering/part-7/receiving-shadows/no-shadows.png)

让我们先关注一下主直射光的阴影。因为这个灯光是包含在base pass中的，所以我们要调整一下。

当主直射光投射阴影时，Unity将寻找一个启用SHADOWS_SCREEN关键字的着色器变体。所以我们必须创建base pass的两个变体，一个有这个关键字，另一个没有。这与VERTEXLIGHT_ON关键字的工作原理相同。

```c
			#pragma multi_compile _ SHADOWS_SCREEN
			#pragma multi_compile _ VERTEXLIGHT_ON
```

这个pas现在有两个multi_compile指令，每个都对应一个关键字。因此，有四种可能的变体。一个没有关键字，一个每个关键字，一个两个关键字。

```c
// Snippet #0 platforms ffffffff:
SHADOWS_SCREEN VERTEXLIGHT_ON

4 keyword variants used in scene:

<no keywords defined>
VERTEXLIGHT_ON
SHADOWS_SCREEN
SHADOWS_SCREEN VERTEXLIGHT_ON
```

在添加了multi_compile预处理指令之后，着色器编译器会报出一个不存在的\_ShadowCoord错误。这是因为 `**UNITY_LIGHT_ATTENUATION**` 宏在运行时阴影时的行为不同。要快速解决这个问题，打开*My Lighting.cginc*文件。当我们有阴影时，将衰减设置为1。

```c
UnityLight CreateLight (Interpolators i) {
	UnityLight light;

	#if defined(POINT) || defined(POINT_COOKIE) || defined(SPOT)
		light.dir = normalize(_WorldSpaceLightPos0.xyz - i.worldPos);
	#else
		light.dir = _WorldSpaceLightPos0.xyz;
	#endif

	#if defined(SHADOWS_SCREEN)
		float attenuation = 1;
	#else
		UNITY_LIGHT_ATTENUATION(attenuation, 0, i.worldPos);
	#endif

	light.color = _LightColor0.rgb * attenuation;
	light.ndotl = DotClamped(i.normal, light.dir);
	return light;
}
```

## 采样阴影

为了得到阴影，我们必须采样屏幕空间阴影纹理。为此，我们需要知道屏幕空间纹理坐标。与其他纹理坐标一样，我们将它们从顶点着色器传递到片段着色器。所以我们需要使用一个额外的插值器来支持阴影。我们先从齐次剪裁空间开始，所以我们需要一个float4。

```c
struct Interpolators {
	…

	#if defined(SHADOWS_SCREEN)
		float4 shadowCoordinates : TEXCOORD5;
	#endif

	#if defined(VERTEXLIGHT_ON)
		float3 vertexLightColor : TEXCOORD6;
	#endif
};

…

Interpolators MyVertexProgram (VertexData v) {
	…

	#if defined(SHADOWS_SCREEN)
		i.shadowCoordinates = i.position;
	#endif

	ComputeVertexLightColor(i);
	return i;
}
```

我们可以通过\_ShadowMapTexture访问屏幕空间的阴影。它是在适当的时候在AutoLight中定义的。简单的方法是使用片段的裁剪空间XY坐标来采样纹理。

```c
UnityLight CreateLight (Interpolators i) {
	…

	#if defined(SHADOWS_SCREEN)
		float attenuation = tex2D(_ShadowMapTexture, i.shadowCoordinates.xy);
	#else
		UNITY_LIGHT_ATTENUATION(attenuation, 0, i.worldPos);
	#endif

	…
}
```

![采样阴影](https://catlikecoding.com/unity/tutorials/rendering/part-7/receiving-shadows/sampling-shadows.png)

我们现在对阴影进行采样，但是使用的是裁剪空间坐标，而不是屏幕空间坐标。我们确实得到了阴影，但它们最终被压缩到屏幕中心的一个小区域。我们得把它们拉长，盖住整个窗口。

在裁剪空间中，所有可见的XY坐标都在−1-1范围内，而屏幕空间的范围是0-1。第一步是将XY减半。接下来，我们还必须偏移坐标，使它们在屏幕的左下角为零。因为我们在处理一个透视变换，我们必须偏移坐标的多少取决于它们之间的距离。在这种情况下，偏移量等于等分之前的第四个齐次坐标。

```c
#if defined(SHADOWS_SCREEN)
		i.shadowCoordinates.xy = (i.position.xy + i.position.w) * 0.5;
		i.shadowCoordinates.zw = i.position.zw;
	#endif
```

![阴影在左下角](https://catlikecoding.com/unity/tutorials/rendering/part-7/receiving-shadows/shadows-corner.png)

这个投影仍然不正确，因为我们用的是齐次坐标。我们必须转换成屏幕空间坐标，用X和Y除以W。

```c
i.shadowCoordinates.xy =
			(i.position.xy + i.position.w) * 0.5 / i.position.w;
```

![不正确的转换](https://catlikecoding.com/unity/tutorials/rendering/part-7/receiving-shadows/incorrect-conversion.png)

结果被扭曲了。影子被拉长和弯曲。这是因为我们在插值之前做除法。这是不正确的，坐标应该独立插值，在除法之前。所以我们必须把除法移动到片段着色器。

```c
Interpolators MyVertexProgram (VertexData v) {
	…

	#if defined(SHADOWS_SCREEN)
		i.shadowCoordinates.xy =
			(i.position.xy + i.position.w) * 0.5; // / i.position.w;
		i.shadowCoordinates.zw = i.position.zw;
	#endif
	
	…
}

UnityLight CreateLight (Interpolators i) {
	…

	#if defined(SHADOWS_SCREEN)
		float attenuation = tex2D(
			_ShadowMapTexture,
			i.shadowCoordinates.xy / i.shadowCoordinates.w
		);
	#else
		UNITY_LIGHT_ATTENUATION(attenuation, 0, i.worldPos);
	#endif

	…
}
```

![正确采样的阴影](https://catlikecoding.com/unity/tutorials/rendering/part-7/receiving-shadows/shadows.png)

此时，你的影子要么看起来是正确的，要么是上下颠倒的。如果它们被翻转，这意味着你的图形API——Direct3D ——屏幕空间Y坐标从0向下到1，而不是向上。要与此同步，请翻转顶点的Y坐标。

```c
i.shadowCoordinates.xy =
			(float2(i.position.x, -i.position.y) + i.position.w) * 0.5;
```

![翻转阴影](https://catlikecoding.com/unity/tutorials/rendering/part-7/receiving-shadows/flipped.png)

## 使用Unity的代码

Unity的包含文件提供了一个函数和宏的集合来帮助我们采样阴影。它们处理API差异和平台限制。例如，我们可以使用UnityCG中的ComputeScreenPos函数。

```c
#if defined(SHADOWS_SCREEN)
		i.shadowCoordinates = ComputeScreenPos(i.position);
	#endif
```

AutoLight包含文件定义了三个有用的宏。它们是SHADOW_COORDS、TRANSFER_SHADOW和shadow_。当启用阴影时，这些宏执行的工作与我们刚才所做的相同。没有影子的时候，他们什么也不做。

SHADOW_COORDS在需要时为阴影坐标定义插值器。它使用_ShadowCoord名称，这是编译器之前所complained的。

```c
struct Interpolators {
	…
	
//	#if defined(SHADOWS_SCREEN)
//		float4 shadowCoordinates : TEXCOORD5;
//	#endif
	SHADOW_COORDS(5)

	…
};
```

TRANSFER_SHADOW在顶点程序中填充这些坐标。

```c
Interpolators MyVertexProgram (VertexData v) {
	…

//	#if defined(SHADOWS_SCREEN)
//		i.shadowCoordinates = i.position;
//	#endif
	TRANSFER_SHADOW(i);

	…
}
```

`**SHADOW_ATTENUATION**`使用坐标对fragment程序中的阴影纹理进行采样。

```c
UnityLight CreateLight (Interpolators i) {
	…

	#if defined(SHADOWS_SCREEN)
		float attenuation = SHADOW_ATTENUATION(i);
	#else
		UNITY_LIGHT_ATTENUATION(attenuation, 0, i.worldPos);
	#endif

	…
}
```

实际上，`**UNITY_LIGHT_ATTENUATION**` 宏已经使用了`**SHADOW_ATTENUATION**`_。这就是为什么我们之前会有编译错误。我们只需要使用这个宏就足够了。唯一的变化是我们必须使用插值器作为它的第二个参数，而我们之前只使用了0。

```c
//	#if defined(SHADOWS_SCREEN)
//		float attenuation = SHADOW_ATTENUATION(i);
//	#else
	UNITY_LIGHT_ATTENUATION(attenuation, i, i.worldPos);
//	#endif
```

在重写代码以使用这些宏之后，我们会得到新的编译错误。这是因为Unity的宏不幸地对顶点数据和插值结构做了假设。首先，假设顶点位置被命名为vertex，而我们将其命名为position。其次，假设插值器的位置被命名为pos，但是我们把它命名为position。

让我们根据实际也采用这些名称。反正它们只在少数地方使用，所以我们不需要做太多改变。

```c
struct VertexData {
	float4 vertex : POSITION;
	…
};

struct Interpolators {
	float4 pos : SV_POSITION;
	…
};

…

Interpolators MyVertexProgram (VertexData v) {
	Interpolators i;
	i.pos = mul(UNITY_MATRIX_MVP, v.vertex);
	i.worldPos = mul(unity_ObjectToWorld, v.vertex);
	…
}
```

我们的阴影应该再次工作了，这一次在Unity支持的所有平台上。

## 多阴影

主直射灯现在投下阴影，但第二方向灯仍然不投阴影。这是因为我们还没有在additive pass中定义SHADOWS_SCREEN。我们可以向它添加一个多编译语句，但是SHADOWS_SCREEN只适用于直射灯光。要获得正确的关键字组合，请将现有的multi-compile语句更改为包含阴影的语句。

```c
#pragma multi_compile_fwdadd_fullshadows
```

这增加了四个关键字的混合，以支持不同的光类型。

```c
// -----------------------------------------
// Snippet #1 platforms ffffffff:
DIRECTIONAL DIRECTIONAL_COOKIE POINT POINT_COOKIE SHADOWS_CUBE SHADOWS_DEPTH
SHADOWS_SCREEN SHADOWS_SOFT SPOT

13 keyword variants used in scene:

POINT
DIRECTIONAL
SPOT
POINT_COOKIE
DIRECTIONAL_COOKIE
SHADOWS_DEPTH SPOT
DIRECTIONAL SHADOWS_SCREEN
DIRECTIONAL_COOKIE SHADOWS_SCREEN
POINT SHADOWS_CUBE
POINT_COOKIE SHADOWS_CUBE
SHADOWS_DEPTH SHADOWS_SOFT SPOT
POINT SHADOWS_CUBE SHADOWS_SOFT
POINT_COOKIE SHADOWS_CUBE SHADOWS_SOFT
```

![两个直射光投射阴影](https://catlikecoding.com/unity/tutorials/rendering/part-7/receiving-shadows/two-shadows.png)

# 聚光灯阴影

现在我们已经处理了直射灯，让我们转向聚光灯。禁用直射灯光，并添加一些聚光灯与阴影的场景。惊喜！由于Unity的宏，聚光灯阴影已经工作。

![两个带有阴影的聚光灯](https://catlikecoding.com/unity/tutorials/rendering/part-7/spotlight-shadows/shadowed-spotlights.png)

在查看帧调试器时，您将看到Unity在聚光灯阴影方面做的工作更少。没有单独的深度pass，也没有屏幕空间的阴影pass。只有阴影纹理被渲染了。

![渲染聚光灯阴影](https://catlikecoding.com/unity/tutorials/rendering/part-7/spotlight-shadows/render-steps.png)

阴影纹理的工作原理与方向灯相同。它们是深度纹理，从光的角度渲染。然而，定向光和聚光灯之间有很大的区别。聚光灯有一个实际的位置，它的光线不平行。因此聚光灯的摄像机有一个视角，不能随意地在周围移动。因此，这些光不能支持阴影 cascades.。

![阴影纹理，近平面设置为4。](https://catlikecoding.com/unity/tutorials/rendering/part-7/spotlight-shadows/shadow-map.png)

虽然相机的设置不同，阴影投射代码是相同的两种光类型。正常的偏移只支持方向的阴影，但它只是为其他灯光设置为零而已。

## 对阴影纹理采样

因为聚光灯不使用屏幕空间的阴影，采样代码必须是不同的。但Unity宏隐藏了这个区别。

**Unity为聚光灯所作的宏是什么样子的？**

阴影坐标通过将顶点位置转换为世界空间，然后从世界空间转换为光的阴影空间而得到的。

```c
#if defined (SHADOWS_DEPTH) && defined (SPOT)
	#define SHADOW_COORDS(idx1) \
		unityShadowCoord4 _ShadowCoord : TEXCOORD##idx1;
	#define TRANSFER_SHADOW(a) a._ShadowCoord = \
		mul(unity_WorldToShadow[0], mul(unity_ObjectToWorld, v.vertex));
	#define SHADOW_ATTENUATION(a) UnitySampleShadowmap(a._ShadowCoord)
#endif
```



我们通过简单地采样屏幕空间阴影贴图来发现直射阴影。Unity在创建这个纹理时考虑了阴影滤波，所以我们不需要担心这个。然而，聚光灯不使用屏幕空间的阴影。因此，如果我们想要使用软阴影，我们必须在片段程序中进行过滤。

然后`**SHADOW_ATTENUATION**`宏使用UnitySampleShadowmap函数采样阴影纹理。这个函数定义包含在AutoLight的UnityShadowLibrary中。当使用硬阴影时，该函数对阴影映射采样一次。当使用软阴影时，它对地图采样四次并对结果取平均值。结果没有用于屏幕空间阴影的过滤效果好，但是速度快得多。

![hard](https://catlikecoding.com/unity/tutorials/rendering/part-7/spotlight-shadows/hard.png)

![soft](https://catlikecoding.com/unity/tutorials/rendering/part-7/spotlight-shadows/soft.png)

# 点光源阴影

现在我们来尝试点光源。当为点光源启用阴影时，将出现编译错误。显然，UnityDecodeCubeShadowDepth是未定义的。这个错误的发生是因为UnityShadowLibrary依赖于UnityCG，但没有明确包含它。所以我们必须确保UnityCG首先被包含在内。我们可以做到这一点，包括UnityPBSLighting之前，包含AutoLight在My lighting着色器中。

```c
#include "UnityPBSLighting.cginc"
#include "AutoLight.cginc"
//#include "UnityPBSLighting.cginc"
```

它通过了编译，但所有对象在光的范围内是黑色的。这应该是阴影贴图出了问题。

![一个不正确的阴影贴图](https://catlikecoding.com/unity/tutorials/rendering/part-7/point-light-shadows/incorrect-shadow-map.png)

当你通过帧调试器检查阴影纹理时，你会发现不是一个，而是六个映射被渲染。这是因为点光源向各个方向照射。因此，阴影纹理必须是一个立方体纹理。立方体贴图是通过渲染场景和摄像机指向六个不同的方向，每个立方体的表面一次。所以在点光源中使用阴影的计算量是很昂贵的。

## 投射阴影

不幸的是，Unity不使用深度立方体贴图。显然，没有足够的平台支持它们。因此，我们不能在My Shadow着色器中依赖于片段的深度值。相反，我们必须输出片段的距离值作为片段程序的结果。

当渲染点光源阴影贴图时，Unity会查找一个定义了SHADOWS_CUBE关键字的阴影投射者变体。SHADOWS_DEPTH关键字用于直射阴影和聚光灯阴影。为了支持这一点，在我们的关卡中添加一个特殊的多编译指令。

```c
Pass {
			Tags {
				"LightMode" = "ShadowCaster"
			}

			CGPROGRAM

			#pragma target 3.0

			#pragma multi_compile_shadowcaster
			
			#pragma vertex MyShadowVertexProgram
			#pragma fragment MyShadowFragmentProgram
			
			#include "My Shadows.cginc"
			
			ENDCG
		}
```

这增加了我们需要的变体。

```c
// Snippet #2 platforms ffffffff:
SHADOWS_CUBE SHADOWS_DEPTH

2 keyword variants used in scene:

SHADOWS_DEPTH
SHADOWS_CUBE
```

因为点光源需要这样一种不同的方法，让我们为它们创建一组单独的程序函数。

```c
#if defined(SHADOWS_CUBE)

#else
	float4 MyShadowVertexProgram (VertexData v) : SV_POSITION {
		float4 position =
			UnityClipSpaceShadowCasterPos(v.position.xyz, v.normal);
		return UnityApplyLinearShadowBias(position);
	}

	half4 MyShadowFragmentProgram () : SV_TARGET {
		return 0;
	}
#endif
```

为了计算出一个片段与光的距离，我们必须构建从光到片段的世界空间向量。我们可以通过为每个顶点创建这些向量。并对它们进行插值来做到这一点。这需要一个额外的插值器。

```c
#if defined(SHADOWS_CUBE)
	struct Interpolators {
		float4 position : SV_POSITION;
		float3 lightVec : TEXCOORD0;
	};

	Interpolators MyShadowVertexProgram (VertexData v) {
		Interpolators i;
		i.position = UnityObjectToClipPos(v.position);
		i.lightVec =
			mul(unity_ObjectToWorld, v.position).xyz - _LightPositionRange.xyz;
		return i;
	}
	
	float4 MyShadowFragmentProgram (Interpolators i) : SV_TARGET {
		return 0;
	}
#else
```

在片段程序中，我们取向量的长度，然后加上偏移。然后我们用 它除以光的范围，使它们适用于0-1的范围。_LightPositionRange.w变量包含范围的倒数，所以我们要乘以这个值。结果作为浮点值输出。

```c
float4 MyShadowFragmentProgram (Interpolators i) : SV_TARGET {
		float depth = length(i.lightVec) + unity_LightShadowBias.x;
		depth *= _LightPositionRange.w;
		return UnityEncodeCubeShadowDepth(depth);
	}
```

**UnityEncodeCubeShadowDepth做了什么？**

Unity更喜欢使用浮点立方体贴图。当这是可能的时候，这个函数什么也不做。当这是不可能的时候，Unity将对值进行编码，因此它被存储在一个8位RGBA纹理的四个通道中。

```c
// Encoding/decoding [0..1) floats into 8 bit/channel RGBA.
// Note that 1.0 will not be encoded properly.
inline float4 EncodeFloatRGBA (float v) {
	float4 kEncodeMul = float4(1.0, 255.0, 65025.0, 16581375.0);
	float kEncodeBit = 1.0 / 255.0;
	float4 enc = kEncodeMul * v;
	enc = frac(enc);
	enc -= enc.yzww * kEncodeBit;
	return enc;
}

float4 UnityEncodeCubeShadowDepth (float z) {
	#ifdef UNITY_USE_RGBA_FOR_POINT_SHADOWS
		return EncodeFloatRGBA(min(z, 0.999));
	#else
		return z;
	#endif
}
```

![正确的纹理贴图](https://catlikecoding.com/unity/tutorials/rendering/part-7/point-light-shadows/correct-shadow-map.png)

## 采样阴影贴图

现在我们的阴影贴图是正确的，点光源阴影出现了。Unity的宏负责这些贴图的采样。

![点光源的阴影](https://catlikecoding.com/unity/tutorials/rendering/part-7/point-light-shadows/shadowed-point-lights.png)

**对于点光源来说，这个宏是什么样子？**

在这种情况下，当投射阴影时，同样的光向量被构造。然后使用这个向量对影子立方体贴图进行采样。注意，插值器只需要三个组件，而不是四个。这次我们不考虑齐次坐标。

```c
#if defined (SHADOWS_CUBE)
	#define SHADOW_COORDS(idx1) \
		unityShadowCoord3 _ShadowCoord : TEXCOORD##idx1;
	#define TRANSFER_SHADOW(a) a._ShadowCoord = \
		mul(unity_ObjectToWorld, v.vertex).xyz - _LightPositionRange.xyz;
	#define SHADOW_ATTENUATION(a) UnitySampleShadowmap(a._ShadowCoord)
#endif
```

在这种情况下，UnitySampleShadowmap采样一个立方体贴图，而不是2D纹理。

```c
samplerCUBE_float _ShadowMapTexture;
inline float SampleCubeDistance (float3 vec) {
	return UnityDecodeCubeShadowDepth(texCUBE(_ShadowMapTexture, vec));
}

inline half UnitySampleShadowmap (float3 vec) {
	float mydist = length(vec) * _LightPositionRange.w;
	mydist *= 0.97; // bias

	#if defined (SHADOWS_SOFT)
		float z = 1.0/128.0;
		float4 shadowVals;
		shadowVals.x = SampleCubeDistance(vec + float3( z, z, z));
		shadowVals.y = SampleCubeDistance(vec + float3(-z,-z, z));
		shadowVals.z = SampleCubeDistance(vec + float3(-z, z,-z));
		shadowVals.w = SampleCubeDistance(vec + float3( z,-z,-z));
		half4 shadows =
			(shadowVals < mydist.xxxx) ? _LightShadowData.rrrr : 1.0f;
		return dot(shadows,0.25);
	#else
		float dist = SampleCubeDistance(vec);
		return dist < mydist ? _LightShadowData.r : 1.0;
	#endif
}
```

和聚光灯阴影一样，对硬阴影阴影贴图采样一次，对软阴影采样四次。最大的区别是Unity不支持对阴影立方体贴图进行滤波。因此，阴影的边缘更加粗糙。所以点光源的阴影既昂贵又有锯齿。

![hard](https://catlikecoding.com/unity/tutorials/rendering/part-7/point-light-shadows/hard.png)

![soft](https://catlikecoding.com/unity/tutorials/rendering/part-7/point-light-shadows/soft.png)

**如何制作漂亮的柔和的灯笼阴影?**

使用一个或多个聚光灯阴影。如果附近没有其他投射阴影的物体，你可以使用没有阴影的带有cookie的光。这对聚光灯和点光源都适用，而且渲染更便宜。

下一个教程是[反射](The next tutorial is [Reflections](https://catlikecoding.com/unity/tutorials/rendering/part-8/).)。

